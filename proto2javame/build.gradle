defaultTasks 'clean', 'build', 'runFindbugs'

usePlugin 'java'
usePlugin 'eclipse'

libDir = 'lib'
libTestDir = libDir + '/test'

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.7'
  testCompile group: 'org.unitils', name: 'unitils', version: '2.4'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.1'
  testCompile fileTree(dir: libTestDir, includes: ['*.jar'])
}

sourceCompatibility = javaVersion
version = jarVersion
manifest.mainAttributes(
        'Main-Class': mainClass
)

compileTestJava.doFirst {
  print 'Compiling Java ME generated source code, this will cause warning messages.'  
}



task runFindbugs << {
  findbugsHome = libDir + '/build/findbugs-1.3.9/'
  findbugsReport = 'doc/findbugs-reports/findbugs-report.txt'

  ant.taskdef(name: 'findbugs', classname: 'edu.umd.cs.findbugs.anttask.FindBugsTask') {
    classpath {
      fileset(dir: findbugsHome + 'lib', includes: '*.jar')
    }
  }

  ant.findbugs(home: findbugsHome, output: 'emacs', outputFile: findbugsReport) {
    sourcepath(path: 'src/main/java')
    'class'(location: 'build/libs/proto2javame-' + jarVersion + '.jar')
  }
}


task generateTestFiles << {
  def protocList = [
          // Creates the Google Protocol Buffer java source file, for integration tests
          'src/test/resources/integration-test/IntegrationTestJavaSe.proto',
          // Creates the updated Google Protocol Buffer java source file, for integration tests
          'src/test/resources/integration-test/UpdatedIntegrationTestJavaSe.proto',
  ]

  def protobufJavaMeList = [
          // Creates the Java ME source code files for unit tests
          'src/test/resources/integration-test/IntegrationTestJavaMe.proto',
          // Creates the updated Java ME source code files for unit tests
          'src/test/resources/integration-test/UpdatedIntegrationTestJavaMe.proto',
          'src/test/resources/tempfiles/JunitTestMain.proto',
          'src/test/resources/tempfiles/JUnitTestLarge.proto'
  ]

  generateProtocFile(protocList)
  generateProtobufJavaMeFile(protobufJavaMeList)
}

task generateTestFilesJavaMeOnly << {
  def list = [
          'src/test/resources/tempfiles/JUnitTestLarge.proto',
          // Creates the Java ME source code files for unit tests
          'src/test/resources/integration-test/IntegrationTestJavaMe.proto',
          // Creates the updated Java ME source code files for unit tests
          'src/test/resources/integration-test/UpdatedIntegrationTestJavaMe.proto',
          'src/test/resources/tempfiles/JunitTestMain.proto',
          'src/test/resources/tempfiles/JUnitTestLarge.proto'
  ]

  generateProtobufJavaMeFile(list)
}


private void generateProtocFile(List protocList) {
  protocList.each {protoFile ->
    print 'protoc: ' + protoFile

    ant.exec(executable: libDir + '/protobuf-compiler/protoc.exe') {
      arg(value: '--java_out=src/test/java')
      arg(value: protoFile)
    }
  }
}

private void generateProtobufJavaMeFile(List protobufJavaMeList) {
  protobufJavaMeList.each {protoFile ->
    print 'protobuf-javame: ' + protoFile

    ant.java(jar: libTestDir + '/' + jarFileName + '-' + jarVersion + '.jar', fork: 'true', failonerror: 'true') {
      arg(value: '--java_out=src/test/java')
      arg(value: protoFile)
    }
  }
}